WHITESPACE = _{ " " | "\t" | "\n" | "\r" }
COMMENT = _{ "//" ~ (!"\n" ~ ANY)* | "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

// Identifiers and basic types
identifier = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }
ascii_text = @{ ASCII_ALPHANUMERIC+ }
number = @{ "-"? ~ ASCII_DIGIT+ }
string = @{ "\"" ~ (!"\"" ~ ANY)* ~ "\"" }
bool = @{ "true" | "false" }
hex_string = @{ "0x" ~ ASCII_HEX_DIGIT+ }
wildcard = @{ "*" }
unit = { "()" }

// Type definitions
type = { "Int" | "Bool" | "Bytes" | identifier }

// Parameters
parameter = { identifier ~ ":" ~ type }
parameter_list = { "(" ~ (parameter ~ ("," ~ parameter)* ~ ","?)? ~ ")" }

// Asset definitions
asset_def = {
    "asset" ~ identifier ~ "=" ~ hex_string ~ "." ~ ascii_text ~ ";"
}

// Party definitions
party_def = {
    "party" ~ identifier ~ ";"
}

// Policy definitions
policy_import = {
    "import" ~ "(" ~ string ~ ")"
}

policy_value = _{ policy_import | hex_string }

policy_def = {
    "policy" ~ identifier ~ "=" ~ policy_value ~ ";"
}

// Type definitions
record_field = { identifier ~ ":" ~ type }

record_def = {
    "type" ~ identifier ~ "{" ~
    (record_field ~ ",")* ~
    "}"
}

variant_case_struct = {
    identifier ~ "{" ~
    (record_field ~ ",")* ~
    "}"
}

variant_case_tuple = {
    identifier ~ "(" ~
    (type ~ ",")* ~
    ")"
}

variant_case_unit = {
    identifier
}

variant_case = _{
    variant_case_struct |
    variant_case_tuple |
    variant_case_unit
}

variant_def = {
    "type" ~ identifier ~ "{" ~
    (variant_case ~ ",")* ~
    "}"
}

type_def = _{
    record_def |
    variant_def
}

// Expressions
property_access = { identifier ~ "." ~ identifier ~ ("." ~ identifier)* }

asset_expr = { asset_term ~ (binary_operator ~ asset_term)* }
asset_term = _{ asset_constructor | property_access | identifier }

asset_constructor = {
    identifier ~ "(" ~ data_expr ~ ("," ~ data_expr)? ~ ")"
}

binary_operator = { "+" | "-" }

data_expr = { data_term ~ (binary_operator ~ data_term)* }
data_term = _{
    "none" |
    unit |
    hex_string |
    number |
    bool |
    string |
    datum_constructor |
    property_access |
    identifier
}

spread_expression = { "..." ~ data_expr }

record_constructor_field = {
    identifier ~ ":" ~ data_expr
}

explicit_variant_case_constructor = {
    "::" ~ identifier ~ "{" ~
    (record_constructor_field ~ ",")* ~
    spread_expression? ~
    "}"
}

implicit_variant_case_constructor = {
    "{" ~
    (record_constructor_field ~ ",")* ~
    spread_expression? ~
    "}"
}

variant_case_constructor = _{
    explicit_variant_case_constructor |
    implicit_variant_case_constructor
}

datum_constructor = {
    identifier ~ variant_case_constructor
}

input_block_from = { "from" ~ ":" ~ data_expr }
input_block_datum_is = { "datum_is" ~ ":" ~ type }
input_block_min_amount = { "min_amount" ~ ":" ~ asset_expr }
input_block_redeemer = { "redeemer" ~ ":" ~ data_expr }
input_block_ref = { "ref" ~ ":" ~ data_expr }

input_block_field = _{ 
    input_block_from |
    input_block_datum_is |
    input_block_min_amount |
    input_block_redeemer |
    input_block_ref
}

input_block = {
    "input" ~ identifier ~ ("*")? ~ "{" ~
    (input_block_field ~ ",")* ~
    "}"
}

output_block_to = { "to" ~ ":" ~ data_expr }
output_block_amount = { "amount" ~ ":" ~ asset_expr }
output_block_datum = { "datum" ~ ":" ~ data_expr }

output_block_field = _{ 
    output_block_to |
    output_block_amount |
    output_block_datum
}

output_block = {
    "output" ~ identifier? ~ "{" ~
    (output_block_field ~ ",")* ~
    "}"
}

mint_block_amount = { "amount" ~ ":" ~ asset_expr }
mint_block_redeemer = { "redeemer" ~ ":" ~ data_expr }

mint_block_field = _{ 
    mint_block_amount |
    mint_block_redeemer
}

mint_block = {
    "mint" ~ "{" ~
    (mint_block_field ~ ",")* ~
    "}"
}

burn_block = {
    "burn" ~ "{" ~
    (mint_block_field ~ ",")* ~
    "}"
}

// Transaction definition
tx_def = {
    "tx" ~ identifier ~ parameter_list ~ "{" ~
    input_block* ~
    burn_block? ~
    mint_block? ~
    output_block* ~
    "}"
}

// Program
program = {
    SOI ~
    (asset_def | party_def | policy_def | type_def | tx_def)* ~
    EOI
}